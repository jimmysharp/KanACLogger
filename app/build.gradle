apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

Properties props = new Properties()
try {
    props.load(file('twitter.properties').newDataInputStream())
} catch (IOException e) {
    throw new GradleException("Missing twitter.properties.")
} catch (IllegalArgumentException e) {
    throw new GradleException("twitter.properties contains illegal argument.")
}
if (props.getProperty("twitterConsumerKey") == null) {
    throw new GradleException("Missing \"twitterConsumerKey\" property in twitter.properties")
}
if (props.getProperty("twitterConsumerSecret") == null) {
    throw new GradleException("Missing \"twitterConsumerSecret\" property in twitter.properties")
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "jimmysharp.kanaclogger"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 4
        versionName "0.0.4-alpha"
    }
    buildTypes {
        debug {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
        }
        release {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:support-v13:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:percent:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation "android.arch.lifecycle:extensions:1.1.0"
    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    implementation "android.arch.lifecycle:common-java8:1.1.0"
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    implementation "android.arch.lifecycle:reactivestreams:1.1.0"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'org.twitter4j:twitter4j-core:4.0.4'
    implementation 'com.annimon:stream:1.1.9'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
repositories {
    mavenCentral()
}
