apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
try {
    props.load(file('twitter.properties').newDataInputStream())
} catch (IOException e) {
    throw new GradleException("Missing twitter.properties.");
} catch (IllegalArgumentException e) {
    throw new GradleException("twitter.properties contains illegal argument.");
}
if (props.getProperty("twitterConsumerKey") == null) {
    throw new GradleException("Missing \"twitterConsumerKey\" property in twitter.properties");
}
if (props.getProperty("twitterConsumerSecret") == null) {
    throw new GradleException("Missing \"twitterConsumerSecret\" property in twitter.properties");
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "jimmysharp.kanaclogger"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 4
        versionName "0.0.4-alpha"
    }
    buildTypes {
        debug {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
        }
        release {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:support-v13:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:percent:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.7.0'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    compile 'org.twitter4j:twitter4j-core:4.0.4'
    compile 'com.annimon:stream:1.1.1'
}
